fn swap(int[] a, int i, int j) {
    int tmp = a[i];
    a[i] = a[j];
    a[j] = tmp;
}

fn quicksort(int[] a, int lo, int hi) {
    int i = lo;
    int j = hi;
    int pivot = a[(lo + hi) / 2];

    while (i <= j) {
        while (a[i] < pivot) {
            i += 1;
        }

        while (a[j] > pivot) {
            j -= 1;
        }

        if (i <= j) {
            swap(a, i, j);
            i += 1;
            j -= 1;
        }
    }

    if (lo < j) {
        quicksort(a, lo, j);
    }

    if (i < hi) {
        quicksort(a, i, hi);
    }
}

fn print_first_and_last(int[] arr, int n) {
    println("First 10:");
    print(arr[0]);
    for (int i = 1; i < 10; i += 1) {
        print(", " + arr[i]);
    }
    println();

    println("Last 10:");
    print(arr[n - 10]);
    for (int i = n - 9; i < n; i += 1) {
        print(", " + arr[i]);
    }
    println();
}

fn main() {
    int n = 1000000;
    int[] arr = new int[n];

    for (int i = 0; i < n; i += 1) {
        arr[i] = random(1000000);
    }
    
    print_first_and_last(arr, n);

    int start = time();
    quicksort(arr, 0, n - 1);
    int end = time();

    println("Sort time: " + (end - start));
    print_first_and_last(arr, n);
}
